--- RCPTT testcase ---
Format-Version: 1.0
Contexts: _f0ltUIayEemuqaKKlJk3rQ,_p5-B4IayEemuqaKKlJk3rQ
Element-Name: UndoRedoTestCase
Element-Type: testcase
Element-Version: 3.0
External-Reference: 
Id: _YZcfsIbZEemuqaKKlJk3rQ
Runtime-Version: 2.3.0.201806262310
Save-Time: 6/4/19 5:21 PM
Testcase-Type: ecl

------=_.content-0a7243a0-75d3-3d5f-9791-539de0e5b7ac
Content-Type: text/ecl
Entry-Name: .content

// Open sessions and compare aird
with [get-view "Project Explorer" | get-tree] {
    select "model1/model1.aird" | double-click
    select "model2/model1.aird" | double-click
    select "model2/model1.aird" "model1/model1.aird"
    get-menu -path "Compare With/Each Other as models" | click
}

// Confirm comparison
get-window "EMF Diff/Merge" | get-button Finish | click
get-window "EMF Diff/Merge" | get-button OK | click

// Export changes
get-editor "Compare ('model1' - 'model1')" | get-button "Export as pending changes" | click
get-window "Defer Merge" | get-button OK | click

// Open the pending file
get-view "Project Explorer" | get-tree | select "model2/model1.pending" | double-click


// Apply one change
get-view "Pending Changes" | get-tree 
    | select "model1 (3)/model1 (3)/System Analysis (2)/System Functions (2)/Root System Function (2)/SystemFunction 1" 
    | get-menu -path "Apply change(s)" | click

// Undo the applied change
get-menu -path "Edit/Undo Merge action" | click

// Verify that the change appears again in the Pending changes view
get-view "Pending Changes" | get-tree | get-item 
    -path "model1 \\(3\\)/model1 \\(3\\)/System Analysis \\(2\\)/System Functions \\(2\\)/Root System Function \\(2\\)/SystemFunction 1" 
    | get-property caption | equals "SystemFunction 1" | verify-true

// Verify that the change does not appear in the Project Explorer
get-view "Project Explorer" | get-tree | get-item 
    -path "model2/\\*model1.aird/model1/System Analysis/System Functions/Root System Function" 
    | get-property childCount | equals 0 | verify-true

// Now ignore one change
get-view "Pending Changes" | get-tree 
    | select "model1 (3)/model1 (3)/EPBS Architecture (1)/Capabilities (1)/CapabilityRealization 1" | get-menu 
    -path "Ignore change(s)" | click

// Undo the ignore 
get-menu -path "Edit/Undo Merge action" | click
//get-menu -path "Edit/Undo Do Command" | click

// Verify the undo works fine
get-view "Pending Changes" | get-tree 
    | select "model1 (3)/model1 (3)/EPBS Architecture (1)/Capabilities (1)/CapabilityRealization 1"
get-view "Pending Changes" | get-tree | get-item 
    -path "model1 \\(3\\)/model1 \\(3\\)/EPBS Architecture \\(1\\)/Capabilities \\(1\\)/CapabilityRealization 1" 
    | get-property caption | equals "CapabilityRealization 1" | verify-true
------=_.content-0a7243a0-75d3-3d5f-9791-539de0e5b7ac--
